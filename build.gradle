buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.6.RELEASE'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.2'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'spring-jpa-rest-service'
    version = '0.1.0'

    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.codehaus.groovy:groovy-all:2.2.2'
    compile 'com.h2database:h2'

    testCompile 'org.spockframework:spock-core'
    testCompile 'org.spockframework:spock-spring'
    testCompile 'org.springframework:spring-test'
    testCompile 'junit:junit'
    testCompile 'org.springframework.restdocs:spring-restdocs:1.0.0.BUILD-SNAPSHOT'
}

ext {
    generatedDocumentation = file('build/generated-snippets')
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', generatedDocumentation
    outputs.dir generatedDocumentation
}

asciidoctor {
    sourceDir 'src/main/asciidoc'
    attributes 'generated': generatedDocumentation
    inputs.dir generatedDocumentation
    dependsOn test
}

task stage(dependsOn: ['bootRepackage'])

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
